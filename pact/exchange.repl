
(begin-tx)
(load "root/fungible-v2.pact")
(load "root/coin.pact")

(env-data
  { 'swap-ns-user: []
  , 'swap-ns-admin: []
  })
(load "ns.pact")
(load "tokens.pact")
(load "exchange.pact")


(define-namespace 'test (sig-keyset) (sig-keyset))
(load "test/ABC.pact")
(commit-tx)

(begin-tx)

;; test pair canonicity
(use swap.exchange)
(expect "canonical pair keys match"
  (get-pair-key test.abc coin)
  (get-pair-key coin test.abc))
(expect "pair does not exist yet" false
  (pair-exists test.abc coin))

(create-pair coin test.abc "")
(env-data {'bob: ["bob"]})
(env-sigs [
  { 'key: "bob"
  , 'caps:
    [(coin.TRANSFER "Bob" (at 'account (get-pair test.abc coin)) 100.0)
     (test.abc.TRANSFER "Bob" (at 'account (get-pair test.abc coin)) 200.0)
    ]
  }])
(test-capability (coin.COINBASE))
(coin.coinbase "Bob" (read-keyset 'bob) 1000.0)
(test.abc.create-account "Bob" (read-keyset 'bob))
(test.abc.fund "Bob" 2000.0)
(add-liquidity coin test.abc
  100.0 200.0 50.0 100.0
  "Bob" (read-keyset 'bob)
  (at 'block-time (chain-data)))

(expect "token liquidity for bob"
  141.321356237309
  (swap.tokens.get-balance (get-pair-key coin test.abc) "Bob")
)
(expect "minimum liquidity for pair"
  MINIMUM_LIQUIDITY
  (swap.tokens.get-balance
    (get-pair-key coin test.abc)
    (at 'account (get-pair coin test.abc)))
)
(expect "kda debited for bob"
  900.0
  (coin.get-balance "Bob")
)
(expect "abc debited for bob"
  1800.0
  (test.abc.get-balance "Bob")
)
;; TODO test event emission
